apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'
apply plugin: 'com.google.firebase.firebase-perf'


android {

    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "dev.olog.msc"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 27002 // android(27) - version code(001)
        versionName "0.11" //update presentation module as well , to mirror effect on aboutActivity
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true
    }

    splits {
        abi {
            enable true
            reset()
            include 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
            universalApk false
        }
    }

    project.ext.versionCodes = ['armeabi-v7a' : 1, 'arm64-v8a' : 2, 'x86' : 3, 'x86_64' : 4]

    android.applicationVariants.all { variant ->
        // assign different version code for each output
        variant.outputs.each { output ->
            output.versionCodeOverride = project.ext.versionCodes.get(
                    output.getFilter(com.android.build.OutputFile.ABI), 0) * 1000000 + android.defaultConfig.versionCode
        }
    }

    buildTypes {

        release {
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled true
            useProguard false
//            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
//            debuggable false
//            applicationIdSuffix = ".debug"
        }
    }

    dataBinding.enabled = true

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dexOptions {
        preDexLibraries true
    }


}


dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // modules
    implementation project(':domain')
    implementation project(':shared')
    implementation project(':presentation')
    implementation project(':music-service')
    implementation project(':shared')
    implementation project(':data')
    implementation project(':floating-info')
    implementation project(':shared-android')

    // data binding
    kapt "com.android.databinding:compiler:$gradle_plugin"

    // kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    // google architecture components
    implementation "android.arch.lifecycle:runtime:1.0.3"
    implementation "android.arch.lifecycle:extensions:$rootProject.micro_architecture"
    implementation "android.arch.lifecycle:common-java8:$rootProject.micro_architecture"

    // support
    implementation "com.android.support:support-v4:$rootProject.support_library"
    implementation "com.android.support:appcompat-v7:$rootProject.support_library"
    implementation "com.android.support:mediarouter-v7:$rootProject.support_library"
    implementation "com.android.support:recyclerview-v7:$rootProject.support_library"
    implementation "com.android.support:cardview-v7:$rootProject.support_library"
    implementation "com.android.support:design:$rootProject.support_library"
    implementation "com.android.support.constraint:constraint-layout:$rootProject.constraint_layout"

    // play service
//    implementation "com.google.android.gms:play-services-cast-framework:$rootProject.play_services"

    // db
    implementation "com.squareup.sqlbrite3:sqlbrite:$rootProject.sql_brite"

    // di
    implementation "com.google.dagger:dagger:$rootProject.dagger"
    kapt "com.google.dagger:dagger-compiler:$rootProject.dagger"
    implementation "com.google.dagger:dagger-android:$rootProject.dagger"
    implementation "com.google.dagger:dagger-android-support:$rootProject.dagger"
    kapt "com.google.dagger:dagger-android-processor:$rootProject.dagger"

    // reactive streams
    implementation "io.reactivex.rxjava2:rxjava:$rootProject.rx_java"
    implementation "io.reactivex.rxjava2:rxandroid:$rootProject.rx_android"
    implementation "com.f2prateek.rx.preferences2:rx-preferences:$rootProject.rx_preferences"

    // other
    implementation "com.sothree.slidinguppanel:library:$rootProject.sliding_panel"
    implementation "com.google.android.exoplayer:exoplayer-core:$rootProject.exo_player"
    implementation "com.airbnb.android:lottie:$rootProject.lottie"
    implementation "org.jsoup:jsoup:$rootProject.jsoup"

    // debug
//    implementation "com.akaita.java:rxjava2-debug:$rootProject.rxjava_debug"
//    debugImplementation "com.squareup.leakcanary:leakcanary-android:$rootProject.leak_canary"
//    debugImplementation "com.amitshekhar.android:debug-db:$rootProject.db_debug"

    // crashlytics
    implementation("com.crashlytics.sdk.android:crashlytics:$rootProject.crashlytics") {
        transitive = true
    }
    implementation "com.google.firebase:firebase-core:$rootProject.firebase"
    implementation "com.google.firebase:firebase-perf:$rootProject.firebase"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}

// leave at bottom
apply plugin: 'com.google.gms.google-services'