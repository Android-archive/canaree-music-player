// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.3.41'


    ext {
        kotlin_version = '1.3.41'
        gradle_plugin = '3.5.0-rc02'
        google_services = '4.3.0'
        fabric = '1.28.1'
        firebase_plugin = '1.1.5'
    }


    repositories {
        google()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.0-rc02'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.google.gms:google-services:$google_services"
        classpath "io.fabric.tools:gradle:$fabric"
        classpath "com.google.firebase:firebase-plugins:$firebase_plugin"
        classpath 'com.google.firebase:perf-plugin:1.3.0'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }

}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url 'https://jitpack.io' }
        maven { url "https://dl.bintray.com/drummer-aidan/maven/" }
        maven { url "https://raw.github.com/fritzlabs/fritz-repository/master" } // fritz
    }
    gradle.projectsEvaluated { // TODO remove after coroutines became stable
        tasks.withType(org.jetbrains.kotlin.gradle.tasks.AbstractKotlinCompile).all {
            kotlinOptions.freeCompilerArgs += ["-Xuse-experimental=kotlinx.coroutines.FlowPreview"]
            kotlinOptions.freeCompilerArgs += ["-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi"]
            kotlinOptions.jvmTarget = "1.8"
        }
    }
    afterEvaluate {
        extensions.findByName('kapt')?.arguments {
            arg("dagger.fastInit", "enabled")
            arg("dagger.formatGeneratedSource", "disabled")
//            arg("dagger.fullBindingGraphValidation", "ERROR")
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext {
    // Sdk and tools
    minSdkVersion = 21
    targetSdkVersion = 29
    compileSdkVersion = 29
    buildToolsVersion = '29.0.0'

    /*  version code
        999 - for old compatibility
        27 - android version
        X.xxx - X major version, xxx minor version
     */
    versionCode = 999_29_3_0_0
    versionName = '3.0.0'

}

apply from: 'dependencies.gradle'
